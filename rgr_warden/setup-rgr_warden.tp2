BACKUP   ~rgr_warden/backup~    // location to store files for uninstall purposes
SUPPORT  ~https://forums.beamdog.com/discussion/84124/mod-ranger-warden-kit#latest~ // email address displayed if install fails
README ~rgr_warden/readme-rgr_warden.html~ // Offer to display the main readme
VERSION ~v1.0.1~

LANGUAGE ~English~
         ~english~
         ~rgr_warden/language/english/setup.tra~ // English




/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////                                                  \\\\\
///// Ranger Warden Kit                                \\\\\
/////                                                  \\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\

BEGIN @1000 
REQUIRE_PREDICATE GAME_IS ~bg2ee eet iwdee bgee~ @1001 /* TOB/EE Check prior to compiling.*/


INCLUDE ~rgr_warden/lib/functions.tpa~
INCLUDE ~rgr_warden/lib/addspellex.tpa~
INCLUDE ~rgr_warden/lib/fl#add_kit_ee.tpa~


// Copying over resources

COPY	~rgr_warden/2da~			~override~
	~rgr_warden/bam~			~override~
	~rgr_warden/eff~			~override~
	~rgr_warden/vvc~			~override~
	~rgr_warden/wav~			~override~
	
	
	ACTION_IF NOT FILE_EXISTS_IN_GAME ~mdtra1.bam~ BEGIN //Checks for bams required for dark treant
		COPY ~rgr_warden/resources/anim/treant/bam~ ~override~
		END
		ACTION_IF NOT FILE_EXISTS_IN_GAME ~tree0a1.wav~ BEGIN //Checks for wavs required for dark treant
		COPY ~rgr_warden/resources/anim/treant/wav~ ~override~
		END

	
//////////////////////////////////////////////////
/////// Fixing set trap animation height /////////
//////////////////////////////////////////////////

COPY_EXISTING ~sptrapbb.vvc~ ~override~
	WRITE_LONG 0x20 (THIS BAND `BIT2)	//remove height adjustment flag to correct set trap animation to overlay properly instead of being up in the air
	WRITE_BYTE 0x2c	0
	BUT_ONLY
	
	
////////////////////////////////////
/////// Adding Projectiles /////////
////////////////////////////////////

ADD_PROJECTILE ~rgr_warden/pro/dtkfbng.pro~
ADD_PROJECTILE ~rgr_warden/pro/dtkfbng1.pro~
ADD_PROJECTILE ~rgr_warden/pro/dtkfbng2.pro~
ADD_PROJECTILE ~rgr_warden/pro/dtkslgs.pro~
ADD_PROJECTILE ~rgr_warden/pro/dtkslgs1.pro~
ADD_PROJECTILE ~rgr_warden/pro/dtkslgs2.pro~
ADD_PROJECTILE ~rgr_warden/pro/dtkfiwb.pro~
ADD_PROJECTILE ~rgr_warden/pro/dtkelcu.pro~
ADD_PROJECTILE ~rgr_warden/pro/dtkelcu1.pro~
	COPY_EXISTING ~dtkelcu.pro~ ~override~
		WRITE_SHORT 0x21a ~%dtkelcu1%~
ADD_PROJECTILE ~rgr_warden/pro/dtkspor.pro~
ADD_PROJECTILE ~rgr_warden/pro/dtkwdxb2.pro~
ADD_PROJECTILE ~rgr_warden/pro/dtkwdxb1.pro~
	COPY_EXISTING ~dtkwdxb1.pro~ ~override~
		WRITE_SHORT 0x21a ~%dtkwdxb2%~


/////////////////////////////////////////////////// 
///////////Adding Spells/Abilities/////////////////
///////////////////////////////////////////////////


////////////Set Traps Bonus per level/////////////

COPY ~rgr_warden/spl/dtkwdtrp.spl~	~override~	//+7 bonus to set traps for level up

			COPY_EXISTING ~dtkwardn.2da~ ~override~		//Adding ability to kit's CLAB-style 2DA 
	SET_2DA_ENTRY 3 2 1  ~AP_dtkwdtrp~
		SET_2DA_ENTRY 3 3 1  ~AP_dtkwdtrp~
			SET_2DA_ENTRY 3 4 1  ~AP_dtkwdtrp~
				SET_2DA_ENTRY 3 5 1  ~AP_dtkwdtrp~
					SET_2DA_ENTRY 3 6 1  ~AP_dtkwdtrp~
						SET_2DA_ENTRY 3 7 1  ~AP_dtkwdtrp~
							SET_2DA_ENTRY 3 8 1  ~AP_dtkwdtrp~
								SET_2DA_ENTRY 3 9 1  ~AP_dtkwdtrp~
									SET_2DA_ENTRY 3 10 1  ~AP_dtkwdtrp~
 PRETTY_PRINT_2DA	
 
 
//////////////Caster Level Penalty//////////////////

COPY ~rgr_warden/spl/dtkwdslv.spl~	~override~	// -2 caster level penalty

			COPY_EXISTING ~dtkwardn.2da~ ~override~		//Adding ability to kit's CLAB-style 2DA 
	SET_2DA_ENTRY 4 1 1  ~AP_dtkwdspl~

 PRETTY_PRINT_2DA


//////////Create Fieldcraft Ammunition///////////

ACTION_IF !FILE_CONTAINS_EVALUATED(~spell.ids~ ~WARDEN_FIELDCRAFT_AMMO~) BEGIN


COPY ~rgr_warden/itm/dtkfbng.itm~	~override~
	LPF ALTER_ITEM_HEADER INT_VAR projectile = EVAL "%dtkfbng%" END	//Arrow item needed for spell
		SAY NAME1 @5000
		SAY NAME2 @5001	
		SAY UNIDENTIFIED_DESC @5002
		SAY DESC @5003
		
COPY ~rgr_warden/itm/dtkfbng1.itm~	~override~
	LPF ALTER_ITEM_HEADER INT_VAR projectile = EVAL "%dtkfbng1%" END	//Bolt item needed for spell
		SAY NAME1 @5004
		SAY NAME2 @5005	
		SAY UNIDENTIFIED_DESC @5006
		SAY DESC @5007
	
COPY ~rgr_warden/itm/dtkfbng2.itm~	~override~
	LPF ALTER_ITEM_HEADER INT_VAR projectile = EVAL "%dtkfbng2%" END	//Bullet item needed for spell
		SAY NAME1 @5008
		SAY NAME2 @5009	
		SAY UNIDENTIFIED_DESC @5010
		SAY DESC @5011
		
COPY ~rgr_warden/itm/dtkslgs.itm~	~override~
	LPF ALTER_ITEM_HEADER INT_VAR projectile = EVAL "%dtkslgs%" END	//Arrow item needed for spell
		SAY NAME1 @5000
		SAY NAME2 @5012	
		SAY UNIDENTIFIED_DESC @5002
		SAY DESC @5013
		
COPY ~rgr_warden/itm/dtkslgs1.itm~	~override~
	LPF ALTER_ITEM_HEADER INT_VAR projectile = EVAL "%dtkslgs1%" END	//Bolt item needed for spell
		SAY NAME1 @5004
		SAY NAME2 @5014	
		SAY UNIDENTIFIED_DESC @5006
		SAY DESC @5015
	
COPY ~rgr_warden/itm/dtkslgs2.itm~	~override~
	LPF ALTER_ITEM_HEADER INT_VAR projectile = EVAL "%dtkslgs2%" END	//Bullet item needed for spell
		SAY NAME1 @5008
		SAY NAME2 @5016	
		SAY UNIDENTIFIED_DESC @5010
		SAY DESC @5017
		
		
COPY ~rgr_warden/spl/dtkfbng.spl~	~override~	//spell effect for disorentation arrow/bullet/bolt
	SAY NAME1 @4003
COPY ~rgr_warden/spl/dtkslgs.spl~	~override~	//spell effect for slumber arrow/bullet/bolt
	SAY NAME1 @4004
	


  ADD_SPELL ~rgr_warden/spl/dtkfcam.spl~ 4 1 WARDEN_FIELDCRAFT_AMMO
    SPRINT current_spell_res "%DEST_RES%"
	SAY NAME1 @3000  // Spell Name
	SAY UNIDENTIFIED_DESC @3001  // Spell description
	
		
			COPY_EXISTING ~dtkwardn.2da~ ~override~		//Adding ability to kit's CLAB-style 2DA 
	SET_2DA_ENTRY 5 1 1  ~GA_%current_spell_res%~
 PRETTY_PRINT_2DA
 
 
COPY ~rgr_warden/spl/dtkfcam1.spl~	~override~
	SAY NAME1 @3002
COPY ~rgr_warden/spl/dtkfcam2.spl~	~override~
	SAY NAME1 @3003
COPY ~rgr_warden/spl/dtkfcam3.spl~	~override~
	SAY NAME1 @3004
	
END


//////////Electrocution Trap//////////

ACTION_IF !FILE_CONTAINS_EVALUATED(~spell.ids~ ~WARDEN_ELECTROCUTION_TRAP~) BEGIN

COPY	~rgr_warden/spl/dtkelcu1.spl~			~override~
	LPF ALTER_SPELL_HEADER INT_VAR projectile = EVAL "%dtkelcu%" END
		LPF ALTER_EFFECT INT_VAR match_opcode = 139 parameter1 = RESOLVE_STR_REF(@4002) END

  ADD_SPELL ~rgr_warden/spl/dtkelcu.spl~ 4 4 WARDEN_ELECTROCUTION_TRAP
    SPRINT current_spell_res "%DEST_RES%"
	SAY NAME1 @3005  // Spell Name
	SAY UNIDENTIFIED_DESC @3006  // Spell description

	
		
			COPY_EXISTING ~dtkwardn.2da~ ~override~		//Adding ability to kit's CLAB-style 2DA 
	SET_2DA_ENTRY 6 4 1  ~GA_%current_spell_res%~
 PRETTY_PRINT_2DA
    
    END
	


/////////////Fieldcraft Poultice//////////////


ACTION_IF !FILE_CONTAINS_EVALUATED(~spell.ids~ ~WARDEN_FIELDCRAFT_POULTICE~) BEGIN

COPY ~rgr_warden/itm/dtkpoul.itm~	~override~	//Detoxifying Poultice
		SAY NAME1 @5018
		SAY NAME2 @5019	
		SAY UNIDENTIFIED_DESC @5020
		SAY DESC @5021
		
	COPY ~rgr_warden/itm/dtkpoul1.itm~	~override~	//Major Detoxifying Poultice
		SAY NAME1 @5018
		SAY NAME2 @5022	
		SAY UNIDENTIFIED_DESC @5020
		SAY DESC @5023
		
		COPY ~rgr_warden/itm/dtkpoul2.itm~	~override~	//Superior Detoxifying Poultice
			SAY NAME1 @5018
			SAY NAME2 @5024
			SAY UNIDENTIFIED_DESC @5020
			SAY DESC @5025


  ADD_SPELL ~rgr_warden/spl/dtkpoul.spl~ 4 1 WARDEN_FIELDCRAFT_POULTICE
    SPRINT current_spell_res "%DEST_RES%"
	SAY NAME1 @3007  // Spell Name
	SAY UNIDENTIFIED_DESC @3008  // Spell description
	
		
			COPY_EXISTING ~dtkwardn.2da~ ~override~		//Adding ability to kit's CLAB-style 2DA 
	SET_2DA_ENTRY 7 1 1  ~GA_%current_spell_res%~
 PRETTY_PRINT_2DA
 
 END


///////////Burning Web Trap////////////
	

ACTION_IF !FILE_CONTAINS_EVALUATED(~spell.ids~ ~WARDEN_BURNING_WEB_TRAP~) BEGIN

COPY	~rgr_warden/spl/dtkfiwb1.spl~			~override~
	LPF ALTER_SPELL_HEADER INT_VAR projectile = EVAL "%dtkfiwb%" END
		LPF ALTER_EFFECT INT_VAR match_opcode = 253 parameter1 = RESOLVE_STR_REF(@4001) parameter2 = 3 END
			LPF ALTER_EFFECT INT_VAR match_opcode = 254 parameter1 = RESOLVE_STR_REF(@4001) END

  ADD_SPELL ~rgr_warden/spl/dtkfiwb.spl~ 4 4 WARDEN_BURNING_WEB_TRAP
    SPRINT current_spell_res "%DEST_RES%"
	SAY NAME1 @3009  // Spell Name
	SAY UNIDENTIFIED_DESC @3010  // Spell description
		
			COPY_EXISTING ~dtkwardn.2da~ ~override~		//Adding ability to kit's CLAB-style 2DA 
	SET_2DA_ENTRY 8 7 1  ~GA_%current_spell_res%~

 PRETTY_PRINT_2DA
    
    END
	

//////////////Parasitic Fungus Trap////////////////


ACTION_IF !FILE_CONTAINS_EVALUATED(~spell.ids~ ~WARDEN_PARASITIC_FUNGUS_TRAP~) BEGIN

COPY	~rgr_warden/spl/dtkspor1.spl~	~override~
LPF ALTER_SPELL_HEADER INT_VAR projectile = EVAL "%dtkspor%" END
	COPY	~rgr_warden/spl/dtkspor2.spl~	~override~


  ADD_SPELL ~rgr_warden/spl/dtkspor.spl~ 4 4 WARDEN_PARASITIC_FUNGUS_TRAP
    SPRINT current_spell_res "%DEST_RES%"
	SAY NAME1 @3011  // Spell Name
	SAY UNIDENTIFIED_DESC @3012  // Spell description
		
			COPY_EXISTING ~dtkwardn.2da~ ~override~		//Adding ability to kit's CLAB-style 2DA 
	SET_2DA_ENTRY 9 10 1  ~GA_%current_spell_res%~

 PRETTY_PRINT_2DA
    
    END
	
	
///////////////Bark Dervish/////////////////////

ACTION_IF !FILE_CONTAINS_EVALUATED(~spell.ids~ ~CLERIC_BARK_DERVISH~) BEGIN

COPY ~rgr_warden/spl/dtkbrdv1.spl~	~override~

	LAF dtk_new_portrait_icon INT_VAR string = RESOLVE_STR_REF(@3013) STR_VAR bam_file = dtkbrdvp RET icon END
	ADD_SPELL ~rgr_warden/spl/dtkbrdv.spl~ 1 3 CLERIC_BARK_DERVISH
		SPRINT current_spell_res "%DEST_RES%"
		SAY NAME1 @3013  // Spell Name
		SAY UNIDENTIFIED_DESC @3014  // Spell description
		LPF ALTER_EFFECT INT_VAR match_opcode = 321 STR_VAR resource = EVAL ~%current_spell_res%~ END
		LPF ALTER_EFFECT INT_VAR match_opcode = 142 parameter2 = icon END
		
			COPY ~rgr_warden/spl/dtkbrdv.spl~	~override~	//modified version for bramblecoat armor effect
			SAY NAME1 @3013  // Spell Name
			SAY UNIDENTIFIED_DESC @3014  // Spell description
			WRITE_BYTE 0x22 0	//removes casting animation
			LPF ALTER_EFFECT INT_VAR match_opcode = 321 STR_VAR resource = dtkbrdv END
			LPF ALTER_EFFECT INT_VAR match_opcode = 142 parameter2 = icon END
			
			///adding spell to joinable Druid NPCs if present
			
				ACTION_IF FILE_EXISTS_IN_GAME ~jaheir7.cre~ BEGIN	//Jaheira
					COPY_EXISTING ~jaheir7.cre~	~override~
					  ADD_KNOWN_SPELL ~%current_spell_res%~ #2 ~priest~ // adds current spell to spellbook
					  //ADD_MEMORIZED_SPELL ~%current_spell_res%~ #2 ~priest~ ( 1 ) // CRE now has 1 instance of current spell memorized
					  BUT_ONLY
					  END
				ACTION_IF FILE_EXISTS_IN_GAME ~jaheir8.cre~ BEGIN	//Jaheira
					COPY_EXISTING ~jaheir8.cre~	~override~
					  ADD_KNOWN_SPELL ~%current_spell_res%~ #2 ~priest~ // adds current spell to spellbook
					  //ADD_MEMORIZED_SPELL ~%current_spell_res%~ #2 ~priest~ ( 1 ) // CRE now has 1 instance of current spell memorized
					  BUT_ONLY
					  END
				ACTION_IF FILE_EXISTS_IN_GAME ~jaheir11.cre~ BEGIN	//Jaheira
					COPY_EXISTING ~jaheir11.cre~	~override~
					  ADD_KNOWN_SPELL ~%current_spell_res%~ #2 ~priest~ // adds current spell to spellbook
					  //ADD_MEMORIZED_SPELL ~%current_spell_res%~ #2 ~priest~ ( 1 ) // CRE now has 1 instance of current spell memorized
					  BUT_ONLY
					  END
				ACTION_IF FILE_EXISTS_IN_GAME ~jaheir12.cre~ BEGIN	//Jaheira
					COPY_EXISTING ~jaheir12.cre~	~override~
					  ADD_KNOWN_SPELL ~%current_spell_res%~ #2 ~priest~ // adds current spell to spellbook
					  //ADD_MEMORIZED_SPELL ~%current_spell_res%~ #2 ~priest~ ( 1 ) // CRE now has 1 instance of current spell memorized
					  BUT_ONLY
					  END
				ACTION_IF FILE_EXISTS_IN_GAME ~jahei14.cre~ BEGIN	//Jaheira
					COPY_EXISTING ~jahei14.cre~	~override~
					  ADD_KNOWN_SPELL ~%current_spell_res%~ #2 ~priest~ // adds current spell to spellbook
					  //ADD_MEMORIZED_SPELL ~%current_spell_res%~ #2 ~priest~ ( 1 ) // CRE now has 1 instance of current spell memorized
					  BUT_ONLY
					  END
					ACTION_IF FILE_EXISTS_IN_GAME ~cernd10.cre~ BEGIN	//Cernd
						COPY_EXISTING ~cernd10.cre~	~override~
						  ADD_KNOWN_SPELL ~%current_spell_res%~ #2 ~priest~ // adds current spell to spellbook
						  //ADD_MEMORIZED_SPELL ~%current_spell_res%~ #2 ~priest~ ( 1 ) // CRE now has 1 instance of current spell memorized
						  BUT_ONLY
						  END
					ACTION_IF FILE_EXISTS_IN_GAME ~cernd12.cre~ BEGIN	//Cernd
						COPY_EXISTING ~cernd12.cre~	~override~
						  ADD_KNOWN_SPELL ~%current_spell_res%~ #2 ~priest~ // adds current spell to spellbook
						  //ADD_MEMORIZED_SPELL ~%current_spell_res%~ #2 ~priest~ ( 1 ) // CRE now has 1 instance of current spell memorized
						  BUT_ONLY
						  END
					ACTION_IF FILE_EXISTS_IN_GAME ~cernd13.cre~ BEGIN	//Cernd
						COPY_EXISTING ~cernd13.cre~	~override~
						  ADD_KNOWN_SPELL ~%current_spell_res%~ #2 ~priest~ // adds current spell to spellbook
						  //ADD_MEMORIZED_SPELL ~%current_spell_res%~ #2 ~priest~ ( 1 ) // CRE now has 1 instance of current spell memorized
						  BUT_ONLY
						  END
					ACTION_IF FILE_EXISTS_IN_GAME ~cernd13b.cre~ BEGIN	//Cernd
						COPY_EXISTING ~cernd13b.cre~	~override~
						  ADD_KNOWN_SPELL ~%current_spell_res%~ #2 ~priest~ // adds current spell to spellbook
						  //ADD_MEMORIZED_SPELL ~%current_spell_res%~ #2 ~priest~ ( 1 ) // CRE now has 1 instance of current spell memorized
						  BUT_ONLY
						  END
					ACTION_IF FILE_EXISTS_IN_GAME ~cernd14.cre~ BEGIN	//Cernd
						COPY_EXISTING ~cernd14.cre~	~override~
						  ADD_KNOWN_SPELL ~%current_spell_res%~ #2 ~priest~ // adds current spell to spellbook
						  //ADD_MEMORIZED_SPELL ~%current_spell_res%~ #2 ~priest~ ( 1 ) // CRE now has 1 instance of current spell memorized
						  BUT_ONLY
						  END
						ACTION_IF FILE_EXISTS_IN_GAME ~faldor5.cre~ BEGIN	//Faldorn
							COPY_EXISTING ~faldor5.cre~	~override~
							  ADD_KNOWN_SPELL ~%current_spell_res%~ #2 ~priest~ // adds current spell to spellbook
							  //ADD_MEMORIZED_SPELL ~%current_spell_res%~ #2 ~priest~ ( 1 ) // CRE now has 1 instance of current spell memorized
							  BUT_ONLY
							  END
			
	END
	
	
ACTION_IF FILE_EXISTS_IN_GAME ~druid10a.bcs~ BEGIN // Adding spell to NPC druid scripts
EXTEND_TOP ~druid10a.bcs~ ~rgr_warden/baf/druidnpc.baf~
END
	ACTION_IF FILE_EXISTS_IN_GAME ~druid10b.bcs~ BEGIN // Adding spell to NPC druid scripts
	EXTEND_TOP ~druid10b.bcs~ ~rgr_warden/baf/druidnpc.baf~
	END
		ACTION_IF FILE_EXISTS_IN_GAME ~druid12a.bcs~ BEGIN // Adding spell to NPC druid scripts
		EXTEND_TOP ~druid12a.bcs~ ~rgr_warden/baf/druidnpc.baf~
		END
			ACTION_IF FILE_EXISTS_IN_GAME ~druid12b.bcs~ BEGIN // Adding spell to NPC druid scripts
			EXTEND_TOP ~druid12b.bcs~ ~rgr_warden/baf/druidnpc.baf~
			END
				ACTION_IF FILE_EXISTS_IN_GAME ~druid14g.bcs~ BEGIN // Adding spell to NPC druid scripts
				EXTEND_TOP ~druid14g.bcs~ ~rgr_warden/baf/druidnpc.baf~
				END
					ACTION_IF FILE_EXISTS_IN_GAME ~bdferrus.bcs~ BEGIN // Adding spell to NPC druid scripts 
					EXTEND_BOTTOM ~bdferrus.bcs~ ~rgr_warden/baf/druidnpc.baf~
					END
						ACTION_IF FILE_EXISTS_IN_GAME ~priest2.bcs~ BEGIN // Adding spell to NPC druid scripts 
						EXTEND_BOTTOM ~priest2.bcs~ ~rgr_warden/baf/druidnp2.baf~
						END
							ACTION_IF FILE_EXISTS_IN_GAME ~priest3.bcs~ BEGIN // Adding spell to NPC druid scripts 
							EXTEND_BOTTOM ~priest3.bcs~ ~rgr_warden/baf/druidnp2.baf~
							END
								ACTION_IF FILE_EXISTS_IN_GAME ~priest4.bcs~ BEGIN // Adding spell to NPC druid scripts 
								EXTEND_BOTTOM ~priest4.bcs~ ~rgr_warden/baf/druidnp2.baf~
								END
				
		
	
////////////////////////////////////
/////// Adding New Items   /////////
////////////////////////////////////


//////////////Trapper's Gauntlets///////////////

COPY ~rgr_warden/itm/dtkwdnbr.itm~	~override~	//Trapper's Gauntlets
		SAY NAME1 @5026
		SAY NAME2 @5027
		SAY UNIDENTIFIED_DESC @5028		
		SAY DESC @5029

		
		ACTION_IF GAME_IS ~bgee~ BEGIN	//adding gauntlets as reward for ancient armor quest in firewine dungeon
			COPY_EXISTING ~knight.dlg~ ~override~
			DECOMPILE_AND_PATCH	BEGIN
			REPLACE_TEXTUALLY	~AddexperienceParty(1500)~	~AddexperienceParty(1500) GiveItemCreate("dtkwdnbr",LastTalkedToBy,1,0,0)~
			END
			COMPILE ~rgr_warden/dlg/knight01.dlg~	
		END
		
			ACTION_IF GAME_IS ~eet~ BEGIN	//adding gauntlets as reward for ancient armor quest in firewine dungeon
				ACTION_IF FILE_EXISTS_IN_GAME	~knight_.dlg~	BEGIN
					COPY_EXISTING ~knight_.dlg~ ~override~
					DECOMPILE_AND_PATCH	BEGIN
					REPLACE_TEXTUALLY	~AddexperienceParty(1500)~	~AddexperienceParty(1500) GiveItemCreate("dtkwdnbr",LastTalkedToBy,1,0,0)~
					END
					COMPILE ~rgr_warden/dlg/knight01.dlg~
				END
			END
			
		ACTION_IF GAME_IS ~bg2ee~ BEGIN
			ACTION_IF FILE_EXISTS_IN_GAME ~ar0305.bcs~ BEGIN	//checks for any existing area script to update (this area doesn't have one by default, doing this helps to future-proof)
			EXTEND_BOTTOM ~ar0305.bcs~ ~rgr_warden/baf/ar0305.baf~	//BG2 - adding to locked chest in thieves guild
			END ELSE BEGIN
				COMPILE ~rgr_warden/baf/ar0305.baf~	//Adding script to Shadow Thief Guild Entrance to spawn Trapper's gauntlets in chest 3
					COPY_EXISTING ~ar0305.are~	~override~
					WRITE_ASCII	0x94 ar0305
					END
		END
			
			ACTION_IF GAME_IS ~iwdee~ BEGIN
				COPY_EXISTING ~rndtres.2da~ ~override~		//IWD Adding to random treasure table for chest in Forgotten God Temple area
					SET_2DA_ENTRY 46 8 1  ~dtkwdnbr~

			PRETTY_PRINT_2DA		
			END
		

//////////////Forest Stalker's Bramblecoat +3//////////////////

ACTION_IF GAME_IS ~bgee bg2ee eet~ BEGIN
		
COPY ~rgr_warden/itm/dtkwdar1.itm~	~override~	//Forest Stalker's Bramblecoat +3
		SAY NAME1 @5030
		SAY NAME2 @5031
		SAY UNIDENTIFIED_DESC @5032		
		SAY DESC @5033
			COPY ~rgr_warden/spl/dtkwdar3.spl~	~override~	//base spell for armor on hit effect
			COPY ~rgr_warden/spl/dtkwdar1.spl~	~override~	//Forest Stalker's Bramblecoat on-hit Barkskin effect
				LPF ALTER_EFFECT INT_VAR match_opcode = 139 parameter1 = RESOLVE_STR_REF(@4005) END
					ACTION_IF NOT FILE_EXISTS_IN_GAME ~alterh.vvc~ BEGIN 
					COPY ~rgr_warden/resources/vvc/alterh.vvc~ ~override~
					COPY ~rgr_warden/resources/bam/alterh.bam~ ~override~
					END
						ACTION_IF NOT FILE_EXISTS_IN_GAME ~#eff_e04.wav~ BEGIN 
						COPY ~rgr_warden/resources/wav/#eff_e04.wav~ ~override~
						END
							ACTION_IF NOT FILE_EXISTS_IN_GAME ~#eff_p07.wav~ BEGIN 
							COPY ~rgr_warden/resources/wav/#eff_p07.wav~ ~override~
							END
			COPY ~rgr_warden/spl/dtkwdar2.spl~	~override~	//Forest Stalker's Bramblecoat on-hit Regeneration effect
			LPF ALTER_EFFECT INT_VAR match_opcode = 139 parameter1 = RESOLVE_STR_REF(@4015) END
END

	ACTION_IF GAME_IS ~bgee~ BEGIN
		COPY_EXISTING ~amaran.cre~ ~override~	//BG1 - adding to Amarande - Cloakwood Treehouse
			REPLACE_CRE_ITEM ~dtkwdar1~ #0 #0 #0 ~NONE~ ~ARMOR~		//Forest Stalker's Bramblecoat +3
			BUT_ONLY
	END
	
		ACTION_IF GAME_IS ~eet~ BEGIN
			COPY_EXISTING ~amaran.cre~ ~override~	//BG1 - adding to Amarande - Cloakwood Treehouse
				REPLACE_CRE_ITEM ~dtkwdar1~ #0 #0 #0 ~NONE~ ~ARMOR~		//Forest Stalker's Bramblecoat +3
				BUT_ONLY
					COPY_EXISTING ~ar0708.are~	~override~	//Cernd's Former Home
					WRITE_LONG 0x4b0 BIT0 + BIT3	//locks the crate armor is placed in, sets trap to reset (will fire every time it's opened)
					WRITE_BYTE 0x4ae 127	//sets skill needed to unlock
					WRITE_BYTE 0x4b8 1		//enables trap for container
					WRITE_BYTE 0x4b4 35		//sets trap detection difficulty
					WRITE_BYTE 0x4b6 127	//set trap removal difficulty
					WRITE_ASCII 0x4d0 ctcl	//sets trap script (call lightning)
						EXTEND_BOTTOM ~ar0708.bcs~ ~rgr_warden/baf/ar0708.baf~	//BG2 - adding to crate in Cernd's former home
		END
		
			ACTION_IF GAME_IS ~bg2ee~ BEGIN
				COPY_EXISTING ~ar0708.are~	~override~	//Cernd's Former Home
					WRITE_LONG 0x4b0 BIT0 + BIT3	//locks the crate armor is placed in, sets trap to reset (will fire every time it's opened)
					WRITE_BYTE 0x4ae 127	//sets skill needed to unlock
					WRITE_BYTE 0x4b8 1		//enables trap for container
					WRITE_BYTE 0x4b4 35		//sets trap detection difficulty
					WRITE_BYTE 0x4b6 127	//set trap removal difficulty
					WRITE_ASCII 0x4d0 ctcl	//sets trap script (call lightning)
						EXTEND_BOTTOM ~ar0708.bcs~ ~rgr_warden/baf/ar0708.baf~	//BG2 - adding to crate in Cernd's former home
			END			
			
//////////////Forest Stalker's Bramblecoat +5  IWDEE//////////////////
ACTION_IF GAME_IS ~iwdee~ BEGIN
COPY ~rgr_warden/itm/dtkwdar3.itm~	~override~	//Forest Stalker's Bramblecoat +5
		SAY NAME1 @5030
		SAY NAME2 @5034
		SAY UNIDENTIFIED_DESC @5032		
		SAY DESC @5035
			COPY ~rgr_warden/spl/dtkwdar5.spl~	~override~	//base spell for armor on hit effect
			COPY ~rgr_warden/spl/dtkwdar1.spl~	~override~	//Forest Stalker's Bramblecoat on-hit Barkskin effect
			LPF ALTER_EFFECT INT_VAR match_opcode = 139 parameter1 = RESOLVE_STR_REF(@4005) END
				ACTION_IF NOT FILE_EXISTS_IN_GAME ~alterh.vvc~ BEGIN 
				COPY ~rgr_warden/resources/vvc/alterh.vvc~ ~override~
				COPY ~rgr_warden/resources/bam/alterh.bam~ ~override~
				END
					ACTION_IF NOT FILE_EXISTS_IN_GAME ~#eff_e04.wav~ BEGIN 
					COPY ~rgr_warden/resources/wav/#eff_e04.wav~ ~override~
					END
						ACTION_IF NOT FILE_EXISTS_IN_GAME ~#eff_p07.wav~ BEGIN 
						COPY ~rgr_warden/resources/wav/#eff_p07.wav~ ~override~
						END
			
			COPY ~rgr_warden/spl/dtkwdar2.spl~	~override~	//Forest Stalker's Bramblecoat on-hit Regeneration effect
			LPF ALTER_EFFECT INT_VAR match_opcode = 139 parameter1 = RESOLVE_STR_REF(@4015) END
				COPY_EXISTING ~kaarund.cre~ ~override~	//adding to Arundel - Kuldahar
				ADD_CRE_ITEM ~dtkwdar3~ #0 #0 #0 ~NONE~ ~ARMOR~		//Forest Stalker's Bramblecoat +5
				BUT_ONLY
END
				
				
//////////////Darkwood Thrasher +3//////////////////
		
COPY ~rgr_warden/itm/dtkentbr.itm~	~override~	//Darkwood Thrasher +3
		SAY NAME1 @5036
		SAY NAME2 @5037
		SAY UNIDENTIFIED_DESC @5038		
		SAY DESC @5039
		
		ACTION_IF GAME_IS ~iwdee~ BEGIN
				COPY_EXISTING ~rndtres.2da~ ~override~		//IWD Adding to random treasure table for sarcophagus in Kresselack's Tomb third floor
					SET_2DA_ENTRY 16 6 1  ~dtkentbr~

			PRETTY_PRINT_2DA		
			END
		
		
//////////////Forest Stalker's Bramblecoat +4//////////////////
ACTION_IF GAME_IS ~bg2ee eet~ BEGIN		
COPY ~rgr_warden/itm/dtkwdar2.itm~	~override~	//Forest Stalker's Bramblecoat +4
		SAY NAME1 @5030
		SAY NAME2 @5040
		SAY UNIDENTIFIED_DESC @5032		
		SAY DESC @5041
			COPY ~rgr_warden/spl/dtkwdar4.spl~	~override~	//base spell for armor on hit effect
END
			

//////////////Forest Stalker's Bramblecoat +5  BG2, EET/////////////////
ACTION_IF GAME_IS ~bg2ee eet~ BEGIN
	COPY ~rgr_warden/itm/dtkwdar3.itm~	~override~	//Forest Stalker's Bramblecoat +5 (restored)
		SAY NAME1 @5030
		SAY NAME2 @5042
		SAY UNIDENTIFIED_DESC @5032		
		SAY DESC @5043
			COPY ~rgr_warden/spl/dtkwdar5.spl~	~override~	//base spell for armor on hit effect
END
			

//////////////Living Entwood//////////////////
ACTION_IF GAME_IS ~bg2ee eet~ BEGIN
	COPY ~rgr_warden/itm/dtkbark.itm~	~override~	//live wood
		SAY NAME1 @5044
		SAY NAME2 @5045
		SAY UNIDENTIFIED_DESC @5046		
		SAY DESC @5047
			COPY ~rgr_warden/itm/dtkdbark.itm~	~override~	//dead wood
			SAY NAME1 @5044
			SAY NAME2 @5052
			SAY UNIDENTIFIED_DESC @5046		
			SAY DESC @5053
			EXTEND_BOTTOM ~baldur.bcs~ ~rgr_warden/baf/entwood.baf~	//Timer for wood to dry out
END
		

//////////////Amber of First Growth//////////////////
ACTION_IF GAME_IS ~bg2ee eet~ BEGIN
	COPY ~rgr_warden/itm/dtkamfg.itm~	~override~
		SAY NAME1 @5048
		SAY NAME2 @5049
		SAY UNIDENTIFIED_DESC @5050		
		SAY DESC @5051
		EXTEND_BOTTOM ~ar2600.bcs~ ~rgr_warden/baf/ar2600.baf~	//Placing into container in Tethir Forest
END
			

//////////////Needler +3////////////			
		
	COPY ~rgr_warden/itm/dtkwdxb1.itm~	~override~
		SAY NAME1 @5054
		SAY NAME2 @5055
		SAY UNIDENTIFIED_DESC @5056		
		SAY DESC @5057
			LPF ALTER_ITEM_HEADER INT_VAR header = 1 projectile = EVAL "%dtkwdxb1%" END
				COPY ~rgr_warden/itm/dtkwdxb2.itm~	~override~	//used up
				SAY NAME1 @5054
				SAY NAME2 @5055
				SAY UNIDENTIFIED_DESC @5056		
				SAY DESC @5058
									
					ACTION_IF GAME_IS ~bgee eet~ BEGIN
					ACTION_IF FILE_EXISTS_IN_GAME ~bd7210.bcs~ BEGIN
						EXTEND_BOTTOM ~bd7210.bcs~ ~rgr_warden/baf/bd7210.baf~	//Corpse in Morentherene's lair
						END
						END
						
					ACTION_IF GAME_IS ~bg2ee eet~ BEGIN
						COPY_EXISTING ~hlketta.cre~ ~override~
						ADD_CRE_ITEM ~dtkwdxb1~ #40 #0 #0 ~NONE~ ~WEAPON~ EQUIP TWOHANDED	//Ketta in Guarded Compound Second Floor
						BUT_ONLY
						END
						
					ACTION_IF GAME_IS ~iwdee~ BEGIN
						EXTEND_BOTTOM ~ar4005.bcs~ ~rgr_warden/baf/ar4005.baf~	//War Chest 6 in Yxunomei's lair
						END


						
		
////////////////////////////////////
/////// Adding Creatures   /////////          
////////////////////////////////////

ACTION_IF GAME_IS ~bg2ee eet~ BEGIN
	ACTION_IF NOT FILE_EXISTS_IN_GAME ~dtksdcre.cre~ BEGIN // Crazed Shadow Druid
	COMPILE ~rgr_warden/baf/dtkcrzdr.baf~
	COPY ~rgr_warden/cre/dtksdcre.cre~ ~override~
	   SAY NAME1 @7000
	   SAY NAME2 @7000
	   WRITE_ASCII 0x248 dtkcrzdr
	   END

	ACTION_IF NOT FILE_EXISTS_IN_GAME ~bdtreant.cre~ BEGIN // check if exists first
	COPY ~rgr_warden/resources/cre/bdtreant.cre~ ~override~	//Dark Treant
	   SAY NAME1 @7001
	   SAY NAME2 @7001
	   END
		ACTION_IF NOT FILE_EXISTS_IN_GAME ~dtkeant1.cre~ BEGIN // check if exists first
		COPY ~rgr_warden/cre/dtkeant1.cre~ ~override~	//Dark Treant variant to hold specific item
		   SAY NAME1 @7001
		   SAY NAME2 @7001
		   END
		   ACTION_IF NOT FILE_EXISTS_IN_GAME ~bdplant.itm~ BEGIN // check if exists first
				COPY ~rgr_warden/resources/itm/bdplant.itm~ ~override~	//defense item for Dark Treant
				SAY NAME1 @8000
				SAY NAME2 @8000
				END
				ACTION_IF NOT FILE_EXISTS_IN_GAME ~bdtreant.itm~ BEGIN // check if exists first
					COPY ~rgr_warden/resources/itm/bdtreant.itm~ ~override~	//attack item for Dark Treant
					SAY NAME1 @8001
					SAY NAME2 @8001
					END
						ACTION_IF NOT FILE_EXISTS_IN_GAME ~bdenshtv.bcs~ BEGIN // Adding script for dark treant if not present
						COMPILE ~rgr_warden/baf/bdenshtv.baf~
						END
							ACTION_IF NOT FILE_EXISTS_IN_GAME ~bdfig00.bcs~ BEGIN // Adding script for dark treant if not present
							COMPILE ~rgr_warden/baf/bdfig00.baf~
							END
END


//////////////Spawning new creatures in game///////////////////

ACTION_IF GAME_IS ~bg2ee eet~ BEGIN
EXTEND_BOTTOM ~ar1400.bcs~ ~rgr_warden/baf/ar1400.baf~	//Crazed Druid + Treants in Restored Shadow Temple Lands
END


/////////////////////////////////////////////
//Adding Cromwell/Cespenar/Conlan Dialogues//
/////////////////////////////////////////////
ACTION_IF GAME_IS ~bg2ee eet~	BEGIN
COMPILE ~rgr_warden/dlg/crom1.d~ //Forest Stalker's Bramblecoat +4
COMPILE ~rgr_warden/dlg/crom2.d~ //Forest Stalker's Bramblecoat +5
COMPILE ~rgr_warden/dlg/crom3.d~ //Needler +3 (Recharge)
	EXTEND_BOTTOM ~ar0334.bcs~ ~rgr_warden/baf/ar0334.baf~		//item generating script for cromwell
COMPILE ~rgr_warden/dlg/cespy1.d~ //Forest Stalker's Bramblecoat +4
COMPILE ~rgr_warden/dlg/cespy2.d~ //Forest Stalker's Bramblecoat +5
COMPILE ~rgr_warden/dlg/cespy3.d~ //Needler +3 (Recharge)
	EXTEND_BOTTOM ~botsmith.bcs~ ~rgr_warden/baf/botsmith.baf~		//item generating script for cespenar
END

ACTION_IF GAME_IS ~iwdee~	BEGIN
COMPILE ~rgr_warden/dlg/conlan.d~	//Needler +3 (Recharge)
END

///////////////////////////////////////////////////////
///////////Adding Ranger: Warden Kit///////////////////
///////////////////////////////////////////////////////
  
  // Internal Name of the Kit
  ADD_KIT ~DTKWARDN~ 
  
   // appended to CLASWEAP.2da
   ~DTKWARDN               1           1           1           1           1           1           1           1~
   // appended column-wise to WEAPPROF.2da
   ~DTKWARDN           3 3 3 3 1 1 3 3 3 3 3 3 3 3 3 3 1 1 3 3 1 1 1 3 3 3 3 3 2 2 2 3 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0~
   // appended to ABCLASRQ.2da
   ~DTKWARDN               13       13       14       0       14      0~
   // appended to ABCLSMOD.2da
   ~DTKWARDN               0      0      0      0       0       -4~
     // appended to ABDCDSRQ.2da
   ~DTKWARDN               69      666      0       0       7      42~
   // appended to ABDCSCRQ.2da
   ~DTKWARDN               0       0       0       0       0      0~
   // appended to ALIGNMNT.2da
   ~DTKWARDN               0       0       0       1       1      1       1       1      1~
   // appended to DUALCLAS.2da
   ~DTKWARDN               0       0       0       0       0       0~
  
   // path to your CLAB-style 2da file
   ~override/dtkwardn.2da~
   
   // PC races and classes allowed to use this kit, from KITTABLE.2da
   ~K_R_H K_R_D K_R_HL K_R_G K_R_HO K_R_E K_R_HE~
  
   // usability flags, added to the end of the KITLIST.2da entry
   ~0x00010000     12~
  
   // HLA table to use; see LUABBR.2da
   ~Ra0~
  
   // list of starting equipment for PCs starting in ToB. Appended column-wise to 25STWEAP.2da
   ~LEAT14 * HELM07 BAG25 RING06 RING31 CLCK02 BOOT01 AMUL19 BRAC16 BELT06 AROW11,40 BULL03,40 BOLT06,40 POTN52,5 POTN4,2 POTN14,5 BOW18 XBOW09 STAF08~
	  
   /* Lower, mixed, and help kit names and descriptions, added to KITLIST.2da */
   SAY @2000
   SAY @2000
   SAY @2001
   
  // Call EE compatibility patch
  LAF fl#add_kit_ee
		INT_VAR
			// CLASTEXT.2DA       BIOGRAPHY
			  biography     = RESOLVE_STR_REF(@2003)
			// CLASTEXT.2DA       BRIEFDESC
			  briefdesc     = RESOLVE_STR_REF(@2003)
			// CLASTEXT.2DA       FALLEN
			//fallen        = ~1~
			// CLASTEXT.2DA       FALLEN_NOTICE
			//fallen_notice = RESOLVE_STR_REF(@2002)
		STR_VAR
			// internal kit name
			kit_name	= ~DTKWARDN~
			// BACKSTAB.2DA       0     1     2     3     4     5     6     7     8     9     10     11     12     13     14     15     16     17     18     19     20     21     22     23     24     25     26     27     28     29     30     31     32     33     34     35     36     37     38     39     40     41     42     43     44     45     46     47     48     49     50
			//backstab	= ~~																																	// Inherit from parent class
			// CLSWPBON.2DA		GETS_PROF_APR       UNARMED_DIVISOR     ZERO_SKILL_THAC0
			//clswpbon	= ~0                   0                   3~																							// Inherit from parent class
			// NUMWSLOT.2DA		SLOTS
			//numwslot	= ~2~																																// Inherit from parent class
			// THIEFSKL.2DA		START_POINTS       LEVEL_POINTS
			//thiefskl	= 			~40					20~																																	// Inherit from parent class
			// TRAPLIMT.2DA		LIMIT
			//traplimt	= ~6~																																	// Inherit from parent class
			// CLASCOLR.2DA METAL     MINOR_CLOTH     MAIN_CLOTH     LEATHER     ARMOR
			//clascolr	= ~66        136             136            66          66~
			// CLASISKL.2DA		START_POINTS     OPEN_LOCKS     FIND_TRAPS     MOVE_SILENTLY     HIDE_IN_SHADOWS     DETECT_ILLUSION     SET_TRAPS
			clasiskl	=		~0                    0             0               0                  0                   0                15~					// Inherit from parent class
			// THIEFSCL.2DA       PICK_POCKETS     OPEN_LOCKS     FIND_TRAPS     MOVE_SILENTLY     HIDE_IN_SHADOWS     DETECT_ILLUSION     SET_TRAPS     STEALTH
			thiefscl	=			~0                0               0              100                100                  0                 100           0~	// Inherit from parent class
			// HPCLASS.2DA		TABLE
			//hpclass		= ~HPROG~																																// Inherit from parent class
			// CLSRCREQ.2DA		HUMAN               ELF                 HALF_ELF            DWARF               HALFLING            GNOME               HALFORC
			//clsrcreq	=		~1                   1                   1                   0                   0                   0                   0~
			// CLASTHAC.2DA		BONUS
			//clasthac	= ~0~																																	// Inherit from parent class
			// SNEAKATT.2DA   0     1     2     3     4     5     6     7     8     9     10     11     12     13     14     15     16     17     18     19     20     21     22     23     24     25     26     27     28     29     30     31     32     33     34     35     36     37     38     39     40     41     42     43     44     45     46     47     48     49     50
			//sneakatt	= ~~
			// CRIPPSTR.2DA 	 0     1     2     3     4     5     6     7     8     9     10     11     12     13     14     15     16     17     18     19     20     21     22     23     24     25     26     27     28     29     30     31     32     33     34     35     36     37     38     39     40     41     42     43     44     45     46     47     48     49     50
			//crippstr	= ~~
	END

/////////Cleaning up 2DAs this mod changes - completely unnecessary, but messy 2DAs bug me///////////

PRINT @1004
	COPY_EXISTING ~clasweap.2da~ ~override~		
					~weapprof.2da~ ~override~
					~abclasrq.2da~ ~override~
					~abclsmod.2da~ ~override~
					~abdcdsrq.2da~ ~override~
					~abdcscrq.2da~ ~override~
					~alignmnt.2da~ ~override~
					~dualclas.2da~ ~override~
					~kittable.2da~ ~override~
					~kitlist.2da~ ~override~
					~25stweap.2da~ ~override~
					~clasiskl.2da~ ~override~
					~thiefscl.2da~ ~override~
					~clastext.2da~ ~override~
					~luabbr.2da~ ~override~
					  
 PRETTY_PRINT_2DA
    


  



